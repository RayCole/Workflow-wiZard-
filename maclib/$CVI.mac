.*
         MACRO ,
&MODL    $CVI
         COPY  $BMCGLBL
         GBLC  &VN
         GBLC  &PACKAGE
.*--------------------------------------------------------------------
.* OBTAIN INFO FROM SYSPARM.
.* EXPECTED SYSPARM FORMAT:
.* "SYSPARM(PRODUCT=XXX,VERSION=NNN,RTI=mmddyy)"
.*--------------------------------------------------------------------
         COPY  SYSPARM$             get glbl and macro def

         SYSPARM PARM=PRODUCT,SUBPARM=GET

         AIF   ( &SYSPARM_FOUND AND K'&SYSPARM_VALUE EQ 3 ).PROD
         MNOTE 8,'PRODUCT not specified or invalid <&SYSPARM_VALUE>'

.PROD    ANOP  ,
&PROD    SETC  '&SYSPARM_VALUE'

         SYSPARM PARM=VERSION,SUBPARM=GET

         AIF   ( &SYSPARM_FOUND AND K'&SYSPARM_VALUE EQ 4 ).VERS
         MNOTE 8,'VERSION not specified or invalid <&SYSPARM_VALUE>'

.VERS    ANOP  ,
&VERS    SETC  '&SYSPARM_VALUE'
         AIF   ( K'&VERS EQ 4 ).VLOK
&VERS    SETC  '&VERS'.'0000'       flesh out to 4 chars
&VERS    SETC  '&VERS'(1,4)         trunc back to 4
.VLOK    ANOP  ,
&VER     SETC  '&VERS'(1,2)         extract version
&REL     SETC  '&VERS'(3,1)         extract release
&MOD     SETC  '&VERS'(4,1)         extract modification level

         SYSPARM PARM=RTI,SUBPARM=GET

         AIF   ( &SYSPARM_FOUND AND K'&SYSPARM_VALUE EQ 6 ).RTI
         MNOTE 8,'RTI not specified or invalid <&SYSPARM_VALUE>'

.RTI     ANOP  ,
&RTI     SETC  '&SYSPARM_VALUE'

         PRINT ON
&PRODUCT SETC  '&PROD'
&PACKAGE SETC  'ENVIRONMENT SERVICES'
&VN      SETC  '&VER..&REL..&MOD'
&DATE1   SETC  '&RTI'
&YR1     SETC  '2004'
         MEND  ,
